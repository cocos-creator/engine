
in vec4 v_shadowPos;

#include <cc-shadow>
#include <packing>

#pragma builtin(global)
layout(set = 0, binding = 4) uniform sampler2D cc_shadowMap;

float CCGetShadowFactorX1 () {
  vec3 clipPos = v_shadowPos.xyz / v_shadowPos.w * 0.5 + 0.5;
  float closestDepth = unpackRGBAToDepth(texture(cc_shadowMap, clipPos.xy));
  float shadow = step(closestDepth, clipPos.z - 0.001);

  return shadow;
}

float CCGetShadowFactorX5 () {
  vec3 clipPos = v_shadowPos.xyz / v_shadowPos.w * 0.5 + 0.5;
  float offsetx = 1.0 / cc_shadowSize.x;
  float offsety = 1.0 / cc_shadowSize.y;
  float shadow = 0.0;
  float closestDepth = 0.0;
  closestDepth = unpackRGBAToDepth(texture(cc_shadowMap, vec2(clipPos.x - offsetx, clipPos.y - offsety)));
  shadow += step(closestDepth, clipPos.z - 0.001);
  closestDepth = unpackRGBAToDepth(texture(cc_shadowMap, vec2(clipPos.x - offsetx, clipPos.y + offsety)));
  shadow += step(closestDepth, clipPos.z - 0.001);
  closestDepth = unpackRGBAToDepth(texture(cc_shadowMap, vec2(clipPos.x, clipPos.y)));
  shadow += step(closestDepth, clipPos.z - 0.001);
  closestDepth = unpackRGBAToDepth(texture(cc_shadowMap, vec2(clipPos.x + offsetx, clipPos.y - offsety)));
  shadow += step(closestDepth, clipPos.z - 0.001);
  closestDepth = unpackRGBAToDepth(texture(cc_shadowMap, vec2(clipPos.x + offsetx, clipPos.y + offsety)));
  shadow += step(closestDepth, clipPos.z - 0.001);

  return shadow /= 5.0;
}

float CCGetShadowFactorX9 () {
  vec3 clipPos = v_shadowPos.xyz / v_shadowPos.w * 0.5 + 0.5;
  float offsetx = 1.0 / cc_shadowSize.x;
  float offsety = 1.0 / cc_shadowSize.y;
  float shadow = 0.0;
  for (int i = -1; i <= 1; i++) {
    for (int j = -1; j <= 1; j++) {
      float closestDepth = unpackRGBAToDepth(texture(cc_shadowMap, clipPos.xy + vec2(i, j) * vec2(offsetx, offsety)));
      shadow += step(closestDepth, clipPos.z - 0.001);
    }
  }

  return shadow /= 9.0;
}

float CCGetShadowFactorX25 () {
  vec3 clipPos = v_shadowPos.xyz / v_shadowPos.w * 0.5 + 0.5;
  float offsetx = 1.0 / cc_shadowSize.x;
  float offsety = 1.0 / cc_shadowSize.y;
  float shadow = 0.0;
  for (int i = -2; i <= 2; i++) {
    for (int j = -2; j <= 2; j++) {
      float closestDepth = unpackRGBAToDepth(texture(cc_shadowMap, clipPos.xy + vec2(i, j) * vec2(offsetx, offsety)));
      shadow += step(closestDepth, clipPos.z - 0.001);
    }
  }

  return shadow /= 25.0;
}
